using sys {
    String,
    Blob,
    StrBuilder
}

class StrBuilder {
    pos = 0;
    reserve(qwords int) {
        size = capacity();
        growStep = size * 2 + 4;
        pos + qwords >= size ?
            insertItems(size, growStep);
    }
    putCh(codePoint int) this {
        reserve(1);
        pos := putChAt(pos, codePoint)
    }
    putStr(s String) this {
        loop !{
            c = s.getCh();
            c != 0 ? putCh(c)
        }
    }
    putOptStr(s ?String) this { s ? putStr(_) }
    putInt(val int) this {
        reserve(3);
        isNeg = val < 0;
        isNeg ? val *= -1;
        p = pos + 21;
        loop {
            set8At(p -= 1, val % 10 + '0');
            (val /= 10) == 0
        };
        isNeg ? set8At(p -= 1, '-');
        copyBytesTo(pos, this, p, pos + 21 - p);
        pos += pos + 21 - p;
    }
    newLine() this {
        putCh(0x0a);
    }
    toStr() @String {
        r = String;
        r.fromBlob(this, 0, pos);
        pos := 0;
        r
    }
}
