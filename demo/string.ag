using sys {
    String;
    Blob;
}

class sys_String{
    get() int { sys_getCh(this) }
}
class Builder {
    +Blob;
    pos = 0;
    reserve(int qwords) {
        size = sys_getSize(this);
        growStep = size * 2 + 4;
        pos + qwords >= size ?
            sys_insertItems(this, size, growStep);
    }
    putCh(int codePoint) this {
        reserve(1);
        pos := sys_putCh(this, pos, codePoint)
    }
    putStr(String s) this {
        loop !{
            c = sys_getCh(s);
            c != 0 ? putCh(c)
        }
    }
    putInt(int val) this {
        reserve(3);
        isNeg = val < 0;
        isNeg ? val *= -1;
        p = pos + 21;
        loop {
            sys_set8At(this, p -= 1, val % 10 + '0');
            (val /= 10) == 0
        };
        isNeg ? sys_set8At(this, p -= 1, '-');
        sys_copyBytes(this, pos, this, p, pos + 21 - p);
        pos += pos + 21 - p;
    }
    newLine() this {
        putCh(0x0a);
    }
    toStr() @String {
        r = String;
        sys_stringFromBlob(r, this, 0, pos);
        pos := 0;
        r
    }
}
