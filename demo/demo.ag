using sdl;
using utils;
using gui;

class Scene{
    +gui_Group;

    sdl = Sdl;
    w = sdl_Window;
    rnd = utils_Random;
    flare = sdl_Texture;
    star = sdl_Texture;
    background = sdl_Texture;
    foreground = sdl_Texture;

    init() ?Scene {
        sdl.initVideo() &&
        w.createCentered("Hello!", 800, 600) &&
        background.init(w, "back.jpg") &&
        foreground.init(w, "fore.png") &&
        flare.init(w, "round.png") &&
        star.init(w, "star.png") ? this
    }
    run() {
        push(10, (i){ +Flare.initAtBottom(this) });
        sdl.eventLoop((e){
            w.blt(background, 0, 0, 800, 600, 0, 0, 800, 600);
            paint(w);
            w.blt(foreground, 0, 0, 800, 600, 0, 0, 800, 600);
            w.flip();
            rnd.get(0, 30) == 0 ?
                push(1, (i){ +Flare.initAtBottom(this) });
            tick(1000/30);
        });
    }
}

class Flare {
    +gui_Actor {
        paint(sdl_Window dst) {
            sprite? {
                _.setColorMod(color);
                _.setAlphaMod(fade >> 8);
                size = fade >> 8 >> 2;
                dst.blt(_, 0, 0, 64, 64, x >> 16 - size/2, y >> 16 - size/2, size, size);
            }
        }
        tick(int milliseconds) bool {
            x := x + dx;
            y := y + dy;
            dy := dy + 1 << 13;
            scene?sc sc.rnd.get(0, 2) == 0 ?
                sc.push(1, (i){ +Sparkles.init(sc, x >> 16, y >> 16, color) });
            fade := fade + dFade;
            dFade := dFade - 140;
            fade == 0 && scene &&s s.rnd.get(0, generation) == 0 ?
                distributeInCircle(s.rnd, s.rnd.get(6, 15), 10 << 16, (dx, dy) {
                    s.push(1, (i){ +Flare.initAtFlare(this, dx, dy) });
                });
            y > -100 << 16 && y <= 600 << 16 && x > 0 && x < 800 << 16 && fade >= 0
        }
    }
    initAtFlare(Flare parent, int dx, int dy) this {
        scene := parent.scene;
        scene? sprite := &_.star;
        x := parent.x;
        y := parent.y;
        color := parent.color;
        generation := parent.generation + 1;
        this.dx := dx;
        this.dy := dy;
    }
    initAtBottom(Scene scene) this {
        this.scene := &scene;
        r = scene.rnd;
        x := r.get(200 << 16, (800-200) << 16);
        dx := r.get(-x / 30, (800 << 16 - x) / 30);
        dy := r.get(-18 << 16, -8 << 16);
        sprite := &scene.flare;
        color := makeBrightColor(r);
    }
    scene = &Scene;
    x = 0;
    dx = 0;
    fade = 0;
    dFade = 4200;
    y = 600 << 16;
    dy = -10 << 16;
    color = 0xff_ff_ff;
    generation = 1;
    sprite = &sdl_Texture;
}

class Sparkles {
    +gui_Actor {
        paint(sdl_Window dst) {
            scene? distributeInCircle(_.rnd, r, r, (x, y){
                dst.fillRect(this.x + x, this.y + y, 1, 1, color);
            })
        }
        tick(int milliseconds) bool { (r := r - 1) > 0 }
    }
    init(Scene scene, int x, int y, int color) this {
        this.scene := &scene;
        this.x := x;
        this.y := y;
        this.color := color;
    }
    setRadius(int r) this { this.r := r; }
    scene = &Scene;
    x = 0;
    y = 0;
    r = 30;
    color = 0;
}

fn makeBrightColor(utils_Random rnd) int {
    r = (rnd.get(-1, 1) & 0xff) |
        (rnd.get(-1, 1) & 0xff << 8) |
        (rnd.get(-1, 1) & 0xff << 16);
    r == 0 ? 0x0ffffff : r
}

fn distributeInCircle(utils_Random rnd, int count, int r, (int, int)void callback) {
    forRange(0, count, (i){
        x = rnd.get(-r, r);
        y = rnd.get(-r, r);
        x*x + y*y < r*r ? callback(x, y);
    });
}

Scene.init()?_.run();

42   // main function returns int for errorlevel TODO: remove
